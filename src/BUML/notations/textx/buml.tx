DomainModel:
  '@startuml'
    elements*=Element
  '@enduml'
  oclConstraints*=Constraint
;

Element:
  SkinParam | Class | Relationship
;

SkinParam:
  'skinparam' 'groupInheritance' group=INT
;

Class:
  isAbstract?='abstract' 'class' name=ID '{'
    classContents*=ClassContent
  '}'
;

Relationship:
  Association | Inheritance
;

Association:
  Bidirectional | Unidirectional | Aggregation | Composition
;

Bidirectional:
  fromClass=[Class] fromCar=Cardinality? '--' toCar=Cardinality? toClass=[Class] ':' name=ID
;

Unidirectional:
  fromClass=[Class] fromCar=Cardinality? fromNav?='<' '--' toNav?='>' toCar=Cardinality? toClass=[Class] ':' name=ID
;

Aggregation:
  fromClass=[Class] fromCar=Cardinality? fromAgg?='o' fromNav?='<' '--' toNav?='>' toAgg?='o' toCar=Cardinality? toClass=[Class] ':' name=ID
;

Composition:
  fromClass=[Class] fromCar=Cardinality? fromComp?='*' fromNav?='<' '--' toNav?='>' toComp?='*' toCar=Cardinality? toClass=[Class] ':' name=ID
;

Inheritance:
  fromClass=[Class] fromInh?='<|' '--' toInh?='|>' toClass=[Class]
;

Cardinality:
  '"' min=CardinalityVal '..'? max=CardinalityVal? '"'
;

CardinalityVal:
  INT | '*'
;

ClassContent:
  Attribute | Method
;

Attribute:
  visibility=Visibility? name=ID ':' type=Type
;

Method:
  visibility=Visibility? modifier?=Modifier 'void'? name=ID '()'
;

Visibility: 
  '#' | '-' | '~' | '+'
;

Type:
  /\w*/
;

Modifier:
  '{static}' | '{abstract}'
;

Constraint:
  'context' context=[Class] 'inv' name=ID ':'
  expression=STRING
;